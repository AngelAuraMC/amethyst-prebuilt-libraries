plugins {
    id("com.android.library")
}

android {
    namespace = "org.angelauramc.imgui_natives"
    compileSdkVersion 36

    defaultConfig {
        minSdk = 21

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles("consumer-rules.pro")
        externalNativeBuild {
            cmake {
                cppFlags("")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled = false
            proguardFiles(
                    getDefaultProguardFile("proguard-android-optimize.txt"),
                    "proguard-rules.pro"
            )
        }
        getByName("debug") {
            jniDebuggable = true
            renderscriptDebuggable = true
        }

    }
    sourceSets {
        main {
            jniLibs.srcDirs += [
                    'jniLibs'
            ]
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
}

afterEvaluate {
    tasks.matching { it.name in [
            "mergeReleaseJniLibFolders",
            "mergeDebugJniLibFolders",
    ] }.configureEach {
        dependsOn(tasks.named("copyImguiNatives"))
    }
}
// Warning: This doesn't check if submodules were cloned! Please ensure they're cloned or it fails with cryptic errors!
tasks.register('buildImguiNatives', Exec) {
    workingDir "$projectDir/imgui-java"
    environment "NDK_HOME", android.ndkDirectory.absolutePath
    commandLine "./buildSrc/scripts/build.sh", "android"
    inputs.files fileTree("$projectDir/imgui") {
        include "imgui-app/**"
        include "imgui-binding/**"
        include "imgui-binding-natives/**"
        include "imgui-binding-lwjgl3/**"
    }
    outputs.dir "/tmp/imgui/dst"
}
tasks.register('copyImguiNatives', Copy) {
    def abis = ["arm64-v8a", "armeabi-v7a", "x86", "x86_64"]
    abis.each { abi ->
        from("/tmp/imgui/dst/${abi}") {
            include "*.so"
            eachFile { fileCopyDetails ->
                // Flatten the directory structure
                System.out.println(fileCopyDetails.getPath())
            }
            into(abi)
        }
    }
    into("$projectDir/jniLibs")
    dependsOn tasks.named("buildImguiNatives")
}
