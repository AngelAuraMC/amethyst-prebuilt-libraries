import java.security.MessageDigest

plugins {
    id 'java'
}

group = 'org.lwjgl.glfw'

configurations.default.setCanBeResolved(true)

jar {
    // Makes it reproducible so the version hash actually does its job
    preserveFileTimestamps = false
    reproducibleFileOrder = true
    // Make sure we actually have something to put in a jar
    def libsDir = file("$projectDir/libs")
    def licensesDir = file("$projectDir/licenses")
    def buildDir = layout.buildDirectory.asFile.get().path
    def versionFile = new File("$buildDir", "libs/version")
    outputs.file(versionFile)
    if (!libsDir.exists() || !libsDir.listFiles()?.length) {
        throw new GradleException("The libs directory is empty or missing: $libsDir")
    }

    if (!licensesDir.exists() || !licensesDir.listFiles()?.length) {
        throw new GradleException("The licenses directory is empty or missing: $licensesDir")
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archiveBaseName = "lwjgl-glfw-classes"

    from {
        configurations.default.collect {
            println(it.getName())
            it.isDirectory() ? it : zipTree(it)
        }
    }
    from("licenses") {
        into './licenses'
    }
    exclude 'net/java/openjdk/cacio/ctc/**'

    doLast {
        def jarFile = archiveFile.get().asFile
        def sha256 = MessageDigest.getInstance("SHA-256")
        jarFile.withInputStream { is ->
            byte[] buffer = new byte[8192]
            int read
            while ((read = is.read(buffer)) != -1) {
                sha256.update(buffer, 0, read)
            }
        }
        def hash = sha256.digest().collect { String.format("%02x", it) }.join()
        versionFile.write(hash)
    }
    outputs.files.each {
        println(it.path)
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
}
