plugins {
    id("com.android.library")
}

android {
    namespace = "org.angelauramc.lwjgl3x"
    compileSdkVersion 36

    defaultConfig {
        minSdk = 21

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles("consumer-rules.pro")
        externalNativeBuild {
            cmake {
                cppFlags("")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled = false
            proguardFiles(
                    getDefaultProguardFile("proguard-android-optimize.txt"),
                    "proguard-rules.pro"
            )
        }
        getByName("debug") {
            jniDebuggable = true
            renderscriptDebuggable = true
        }

    }
    sourceSets {
        main {
            jniLibs.srcDirs += [
                    layout.buildDirectory.dir("generated/jniLibs")
            ]
            assets.srcDirs += [
                    layout.buildDirectory.dir("generated/assets"),
                    "assets"
            ]
        }

    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
}

afterEvaluate {
    tasks.matching { it.name in [
          "mergeReleaseJniLibFolders",
          "mergeDebugJniLibFolders",
          "mergeReleaseAssets",
          "mergeDebugAssets"
    ] }.configureEach {
      dependsOn(tasks.named("buildLwjglAar"))
    }
}

tasks.register("buildLwjglAar"){
    // dependsOn tasks.named("getLwjglJar")
    dependsOn tasks.named("buildLwjglNatives")
}

tasks.register("buildLwjglNatives", Exec) {
    workingDir "$projectDir"
    environment "NDK_HOME", android.ndkDirectory.absolutePath
    commandLine "bash", "build-lwjgl-natives.sh", "android", 'armeabi-v7a' ,'arm64-v8a' , 'x86', 'x86_64'
    inputs.files fileTree("$projectDir/../lwjgl3") {
      include "modules/**"
    }
    outputs.dir layout.buildDirectory.dir("generated/jniLibs/")
    outputs.dir layout.buildDirectory.dir("generated/vulkanmod/jniLibs/")
    dependsOn tasks.named("prepareLwjglBuildEnvironment")
    // AGP requires Java 11+ so we can assume we don't have Java 8 set to JAVA_HOME
    // and only check for JAVA8_HOME
    onlyIf { System.getenv("JAVA8_HOME") != null }
    ignoreExitValue = true
}

tasks.register("prepareLwjglBuildEnvironment", Exec) {
    workingDir "$projectDir"
    environment "NDK_HOME", android.ndkDirectory.absolutePath
    commandLine "bash", "prepare-lwjgl-build-environment.sh"
    outputs.dir("$projectDir/../lwjgl3/freetype")
    outputs.dir("$projectDir/../lwjgl3/libffi")
    outputs.dir("$projectDir/../lwjgl3/bin/libs/native/linux")
    dependsOn tasks.named("getOpenALNativeAll")
    onlyIf { System.getenv("JAVA8_HOME") != null }
}

tasks.register("getLwjglJar", Copy) {
    def buildLwjglJar = project(":jre_lwjgl3glfw").tasks.named("jar")
    dependsOn(buildLwjglJar)
    from(buildLwjglJar.map { it.outputs.files })
    into(layout.buildDirectory.dir("generated/assets/components/lwjgl3"))
}

tasks.register("getOpenALNativeAll") {
    dependsOn(
        tasks.matching { it.name.startsWith("getOpenALNative_") }
    )
}

def abiTargets = [
        "arm64-v8a": file("$projectDir/../lwjgl3/bin/libs/native/linux/arm64/org/lwjgl/openal"),
        "armeabi-v7a": file("$projectDir/../lwjgl3/bin/libs/native/linux/arm32/org/lwjgl/openal"),
        "x86_64"   : file("$projectDir/../lwjgl3/bin/libs/native/linux/x64/org/lwjgl/openal"),
        "x86"      : file("$projectDir/../lwjgl3/bin/libs/native/linux/x86/org/lwjgl/openal")
]

abiTargets.each { abi, targetDir ->
    tasks.register("getOpenALNative_${abi}", Copy) {
        def bundleAar = project(":openal-soft").tasks.named("bundleReleaseAar")
        dependsOn(bundleAar)
        from(bundleAar.map { zipTree(it.outputs.files.singleFile) }) {
            include("jni/${abi}/*.so")
            eachFile { f ->
                f.path = f.name
            }
            includeEmptyDirs = false
        }
        into(targetDir)
    }
}

tasks.named("clean", Delete) {
    delete (
        "$projectDir/../lwjgl3/freetype",
        "$projectDir/../lwjgl3/libffi",
        "$projectDir/../lwjgl3/bin/libs/native"
    )
}

if (!System.getenv('JAVA8_HOME')) {
    logger.error("""
    ================= LWJGL ERROR =================
    JAVA8_HOME is not set.
    LWJGL can't be built without Java 8.
    Natives will NOT BE BUILT.
    ==============================================
    """.stripIndent())
}
