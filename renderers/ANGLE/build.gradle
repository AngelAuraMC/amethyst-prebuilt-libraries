plugins {
    id("com.android.library")
}

android {
    namespace = "org.angelauramc.angle"
    compileSdkVersion 36

    defaultConfig {
        minSdk = 21

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles("consumer-rules.pro")
        externalNativeBuild {
            cmake {
                cppFlags("")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled = false
            proguardFiles(
                    getDefaultProguardFile("proguard-android-optimize.txt"),
                    "proguard-rules.pro"
            )
        }
        getByName("debug") {
            jniDebuggable = true
            renderscriptDebuggable = true
        }

    }
    sourceSets {
        main {
            jniLibs.srcDirs += [
                    layout.buildDirectory.dir("generated/jniLibs")
            ]
            assets.srcDirs += [
                    'assets'
            ]
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
}

afterEvaluate {
    tasks.matching { it.name in [
            "mergeReleaseJniLibFolders",
            "mergeDebugJniLibFolders",
    ] }.configureEach {
        dependsOn(tasks.named("buildAngleNatives"))
    }
}

tasks.register('buildAngleNatives', Exec) {
    workingDir "$projectDir"
    environment "NDK_HOME", android.ndkDirectory.absolutePath
    commandLine("bash", "./build-angle-android.sh")
    inputs.files fileTree("$projectDir/angle") {
        include "src/**"
    }
    outputs.dir layout.buildDirectory.dir("generated/jniLibs")
}


tasks.register("cleanAngleNatives", Delete) {
    def abis = ["arm64-v8a", "armeabi-v7a", "x86", "x86_64"]
    abis.each { abi ->
        delete layout.buildDirectory.dir("generated/jniLibs/${abi}")
        delete fileTree("$projectDir/angle/out/android-${abi}") {
            exclude "args.gn"
        }
    }
}

tasks.named("clean") {
    dependsOn(tasks.named("cleanAngleNatives"))
}

