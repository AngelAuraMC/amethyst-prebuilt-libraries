plugins {
    id("com.android.library")
}

android {
    namespace = "org.angelauramc.mesa_zink_kopper"
    compileSdkVersion 36

    defaultConfig {
        minSdk = 24

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles("consumer-rules.pro")
        externalNativeBuild {
            cmake {
                cppFlags("")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled = false
            proguardFiles(
                    getDefaultProguardFile("proguard-android-optimize.txt"),
                    "proguard-rules.pro"
            )
        }
        getByName("debug") {
            jniDebuggable = true
            renderscriptDebuggable = true
        }
        create("fordebug") {
            jniDebuggable = true
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs += [
                    'jniLibs'
            ]
        }
    }
    externalNativeBuild {
        cmake {
            path("GLXShim/src/main/cpp/CMakeLists.txt")
            version = "3.22.1"
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
}

afterEvaluate {
    tasks.named("mergeReleaseJniLibFolders") {
        dependsOn(tasks.named("copyZink"))
    }
    tasks.named("mergeDebugJniLibFolders") {
        dependsOn(tasks.named("copyZink"))
    }
}

// TODO: Add a way to swap between debug and release builds
tasks.register('buildZink', Exec) {
    workingDir "$projectDir/builddirs"
    commandLine "./build_zink_builddirs.sh", android.ndkDirectory.absolutePath
    inputs.files fileTree("$projectDir") {
        include "crossfiles/*.sh"
        include "builddirs/*.sh"
        include "drm/**"
        include "GLXShim/**"
    }
    outputs.dir "$projectDir/kopper_zink_install_dir"
    finalizedBy tasks.named("copyZink")
}

tasks.register('copyZink', Copy) {
    def abis = ["arm64-v8a", "armeabi-v7a", "x86", "x86_64"]
    abis.each { abi ->
        from("$projectDir/kopper_zink_install_dir/kopper_zink-${abi}-android/lib") {
            include "*.so"
            eachFile { fileCopyDetails ->
                // Flatten the directory structure
                System.out.println(fileCopyDetails.getPath())
            }
            rename("zink_dri.so", "libzink_dri.so")
            exclude "libGLESv*"
            into(abi)
        }
        // Special handling for zink_dri.so
        from("$projectDir/kopper_zink_install_dir/kopper_zink-${abi}-android/lib/dri") {
            include "*.so"
            eachFile { fileCopyDetails ->
                // Flatten the directory structure
                System.out.println(fileCopyDetails.getPath())
            }
            rename("zink_dri.so", "libzink_dri.so")
            into(abi)
        }
    }
    into("$projectDir/jniLibs")
    dependsOn tasks.named("buildZink")
}