plugins {
    id 'com.android.library' version libs.versions.agp apply false
    id 'org.jetbrains.kotlin.android' version libs.versions.kotlin apply false
    id 'base'
}

tasks.register("collectAars", Copy) {
    from(subprojects.collect { subproject ->
        subproject.layout.buildDirectory.dir("outputs/aar")
    })
    into(layout.buildDirectory.dir("outputs/aar"))
    outputs.dir(layout.buildDirectory.dir("outputs/aar"))
}

tasks.register("collectLibs", Copy) {
    from(subprojects.collect { subproject ->
        subproject.layout.buildDirectory.dir("libs/")
    })
    into(layout.buildDirectory.dir("libs/"))
    outputs.dir(layout.buildDirectory.dir("libs/"))
}

tasks.register("collectLibLicenses", Copy) {
    from(subprojects.collect { subproject ->
        subproject.layout.buildDirectory.dir("licenses/")
    })
    into(layout.buildDirectory.dir("licenses/"))
    outputs.dir(layout.buildDirectory.dir("licenses/"))
}

tasks.named("clean", Delete) {
    delete(
        layout.buildDirectory.dir("outputs/aar"),
        layout.buildDirectory.dir("libs"),
        layout.buildDirectory.dir("licenses")
    )
}

tasks.named("collectAars") {
    dependsOn(tasks.named("collectLibs"))
    dependsOn(tasks.named("collectLibLicenses"))
}

subprojects { subproject ->
    afterEvaluate {
        // Makes sure collectAars only does release and debug
        tasks.configureEach { task ->
            if (task.name.startsWith("bundle") &&
                    !task.name.endsWith("ReleaseAar") &&
                    !task.name.endsWith("DebugAar")) {

                task.enabled = false
                logger.lifecycle("Disabled task: ${task.name}")
            }
        }
        // Makes sure AARs are collected from subprojects at the right time
        tasks.matching { it.name in ["assembleRelease", "assembleDebug"] }.configureEach {
            finalizedBy(rootProject.tasks.named("collectAars"))
        }
        // Makes sure AARs are deleted from subprojects at the right time
        tasks.matching { ["bundleReleaseAar", "bundleDebugAar"].any {
            prefix -> it.name.startsWith(prefix)
        } }.configureEach {
            mustRunAfter(rootProject.tasks.named("clean"))
        }
        tasks.matching { ["buildNdkBuildRelease", "buildNdkBuildDebug"].any {
            prefix -> it.name.startsWith(prefix)
        } }.configureEach {
            finalizedBy(rootProject.tasks.named("clean"))
        }
        // Makes sure JARs are put in top level build folder on build
        tasks.matching { it.name == 'jar' }.configureEach {
            finalizedBy(rootProject.tasks.named('collectLibs'))
            finalizedBy(rootProject.tasks.named('collectLibLicenses'))
        }
        // Force SDK21 on all of them!!
        if (subproject.plugins.hasPlugin('com.android.library')) {
            subproject.android {
                defaultConfig {
                    minSdkVersion 21
                }
            }
        }
    }
}


